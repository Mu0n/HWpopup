plugins {
    id "de.undercouch.download" version "3.2.0"
}

apply plugin: 'java'

sourceSets {
    main {
        java {
            srcDirs = ['src/mic'] // Consider moving src/mic to src/main/java
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url "http://www.vassalengine.org/maven/"
    }
    maven {
        url "http://maven.geotoolkit.org"
    }
}

sourceCompatibility = '11'
targetCompatibility = '11'

dependencies {
    compileOnly 'org.slf4j:slf4j-api:1.7.7'
    compile 'commons-codec:commons-codec:1.6'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.1.5'
    compile group: 'com.google.guava', name: 'guava-jdk5', version: '17.0'
    compile group: 'org.vassalengine', name: 'vassal-app', version: '3.4.7'
}

task unpackVmod(type: Copy) {
    def outputDir = file("${buildDir}/swxwmg.vmod-unpacked")

    from zipTree(file('build/distributions/swxwmg.vmod'))
    exclude '**/*.class', 'META-INF/', 'com/google/**/*', 'com/fasterxml/**/*'
    includeEmptyDirs = false
    into outputDir
}

task buildVmod(type: Zip) {
    archiveName 'swxwmg.vmod'
    duplicatesStrategy 'exclude'
    from 'swxwmg.vmod-unpacked'
    from compileJava
    for (dependency in (configurations.compile).files) {
        // Need to extract jars so vassal can find classes
        from zipTree(dependency)
    }
    exclude "**/*.jar"
    doLast {
        println "Compiled vmod at ${archivePath}"
    }
}
